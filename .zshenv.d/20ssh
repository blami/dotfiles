#~/.zshenv.d/20ssh - SSH environment

zmodload zsh/stat

# check directory and file permissions
for i in .ssh .ssh/keys .ssh/config .ssh{,/keys}/id_*[^.pub](N); do
	zstat -H st -o "$HOME/$i"
	[[ -f "$HOME/$i" && ${st[mode]:(-3)} != "600" ]] && chmod 600 "$HOME/$i"
	[[ -d "$HOME/$i" && ${st[mode]:(-3)} != "700" ]] && chmod 700 "$HOME/$i"
done ; unset i st

# only interactive shells
[[ $- == *i* ]] || return

# ssh-agent
# run agent or reuse running agent's $SSH_AUTH_SOCKET
whence -p ssh-agent >/dev/null || return
SSH_AUTH_SOCK=
SSH_AGENT_PID=

_ssh_agent_readenv() {
	[[ -r $XDG_RUNTIME_DIR/ssh-agent ]] || return
	local l
	for l in ${(f@)"$(<$XDG_RUNTIME_DIR/ssh-agent)"}; \
		[[ $l =~ '^(SSH_[A-Z_]+)=([^;]+)' ]] && : ${(P)match[1]::=${match[2]}}
}
_ssh_agent_ps() {
	local l
	# WARN: this must work across platforms
	for l in ${(f@)"$(ps -u $USER -o pid,comm)"}; \
		[[ $l == [[:blank:]]#$1[[:blank:]]##"ssh-agent" ]] && return 0
	return 1	
}

_ssh_agent_readenv
[[ -n $SSH_AGENT_PID ]] && _ssh_agent_ps $SSH_AGENT_PID || {
	ssh-agent -s >$XDG_RUNTIME_DIR/ssh-agent
	_ssh_agent_readenv
}

unfunction _ssh_agent_readenv _ssh_agent_ps
export SSH_AUTH_SOCK SSH_AGENT_PID


# vim:set ft=zsh:
