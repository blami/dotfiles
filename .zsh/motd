#!/usr/bin/zsh
# ~/.zsh/motd - gather fortunes from file(s), pick and print random one

# Usage: motd /path/to/file [/path/to/other_file ...]

zmodload zsh/mapfile

# TODO: Review all this
# TODO: Add function to add fortune cookie
# TODO: Rename to fortune?

motd_box () {
    quote=( ${(s.\n.)"${1}"} )
    local size=0
    for line in $quote; do
        [ ${#line} -gt $size ] && size=${#line}
    done
    size=$((size+4))
    [ $size -gt $COLUMNS ] && size=$COLUMNS
    printf "-%.0s" {1..$size}
    printf "\n"
    for line in $quote; do
        printf "  $line\n"
    done
    printf "-%.0s" {1..$size}
    printf "\n"
}

motd () {
    local db=()
    local quote

    for arg in $@; do
        # Process files
        local file=( "${(f)mapfile[$arg]}" )
        for line in $file; do
            if [ "${line// /}" = "%" ]; then
                [ ! -z "$quote" ] && db+=("$quote")
                quote=""
                continue
            fi
            [ -z "$quote" ] && quote="$line" || quote="$quote\n$line"
        done
    done

    # There are no quotes in database, exit
    [ ${#db[@]} -ne 0 ] || return
    # Pick random quote and print it
    quote=$db[${RANDOM}%${#db}+1]

    motd_box $quote
    #print "\n$quote\n"
}

motd $@
