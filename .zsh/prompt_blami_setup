# Created by blami <blami@blami.net>

# Prompt components explanation:
#
#  .- 24h time
#  |     .- date in format DD.MM
#  |     | .- day of the week (weekend is red)
#  |     | |
#  |     | |     .- user
#  |     | |     |   .- @ is underline for SSH connection
#  |     | |     |   |.- host (HOST_NICKNAME if set)
#  |     | |     |   ||   .- host health/readiness
#  |     | |     |   ||   |.- host OS
#  |     | |     |   ||   ||   .- environment (.env)
#  |     | |     |   ||   ||   |     |  .- shortened path
#  |     | |     |   ||   ||   |     |  |        .- vcs info
#  v     v v     v   vv   vv   v     v  v        v
# (23:55 31w12)-(user@host●L)-([env:][@]~..di/r[ ±master])
# [255 ][&2 ][@]dir $ _                                                 -I-
# ^     ^    ^  ^   ^
# |     |    |  |   `- pound (EUID=0 is #)
# |     |    |  `- last path component
# |     |    `- symlink
# |     `- background jobs
# `- non-zero exit code


# Dependencies
zmodload -i zsh/datetime
autoload -Uz vcs_info
autoload -Uz cwdlast cwdellipse

# {{{ Vi-mode status
# NOTE Need to set RPROMPT for zle-line-init (first prompt)
RPROMPT=" "

function zle-line-init zle-keymap-select {
    # Color setup for outlines
    local C_O=${prompt_blami_color0}
    [ $EUID -eq 0 ] && C_O=${prompt_blami_color1}

    case $KEYMAP in
        vicmd)
            RPROMPT="%B%F{$C_O}N%f%b"
            print -rn -- $terminfo[cvvis]
            ;;
        viins|main)
            RPROMPT="%B%F{green}I%f%b"
            print -rn -- $terminfo[cnorm]
            ;;
    esac
    zle reset-prompt
    zle && zle -R
}

TRAPWINCH() {
  zle reset-prompt
  zle &&  zle -R
}

zle -N zle-line-init
zle -N zle-keymap-select
# NOTE This is already done in .zshrc
#export KEYTIMEOUT=1
# }}}


prompt_blami_precmd() {
    setopt localoptions noxtrace

    # Color setup for outlines
    local C_O=${prompt_blami_color0}
    [ $EUID -eq 0 ] && C_O=${prompt_blami_color1}

    # New-line
    local NL=$'\n'
    # Section begin, end and separator
    local SB="%B%F{${C_O}}(%f%b"
    local SE="%B%F{${C_O}})%f%b"
    local SS="%B%F{${C_O}}-%f%b"

    PS1=${NL}

    # Date
    # ----
    local dow="umtwhfs" ; dow=${dow:$(strftime %w $EPOCHSECONDS):1}
    case $dow in
        s|u) dow="%F{magenta}${dow}%f" ;;
        *) dow="%F{cyan}${dow}%f" ;;
    esac
    # NOTE No need to bold $dow as whole line is made bold
    PS1+="${SB}%B%F{${C_O}}%D{%H:%M %d}%f${dow}%F{$C_O}%D{%m}%f%b${SE}"

    # User and hostname
    # -----------------
    local host=${HOSTNICK:-$HOST}
    local title_host=$host
    if [ ! -z $SSH_CONNECTION ]; then
        host="%U@%u${host}"
        title_host="@${title_host}"
    else
        host="@${host}"
    fi
    local host_ok=
    case $HOSTOK in
        0) host_ok="%F{green}●%f" ;;
        1) host_ok="%F{red}●%f" ;;
    esac
    local host_os="%F{white}${OS:0:1:u}%f"
    # NOTE No need to bold host_ok as whole line is made bold
    PS1+="${SS}${SB}%B%F{green}%n${host}%f${host_ok}${host_os}%b${SE}"

    # Working directory
    # -----------------
    # NOTE This is applied below in Working directory name section too
    local cwd_ln=
    [ -L $PWD ] && cwd_ln="@"
    local cwd_color=cyan
    [ ! -w $PWD ] && cwd_color=magenta
    local cwd_name=$(cwdlast)

    # Handle prompt length better, this can still overlap
    if [ $COLUMNS -gt 80 ]; then
        # Environment (.env)
        local env=

        # Current working directory elipsized
        local cwd="%F{$cwd_color}${cwd_ln}$(cwdellipse)%f"

        # VCS
        local vcs=
        #vcs_info
        #[ ! -z ${vcs_info_msg_0_} ] && vcs=" ${vcs_info_msg_0_}"

        PS1+="${SS}${SB}%B${env}${cwd}${vcs}%b${SE}"
    fi

    PS1+=${NL}

    # Return value
    # ------------
    PS1+="%(?..%B%F{red}%? %f%b)"

    # Background jobs
    # ---------------
    PS1+="%1(j.%B%F{$C_O}&%j %f%b.)"

    # Working directory name
    # ----------------------
    PS1+="%B%F{$cwd_color}${cwd_ln}${cwd_name}%f%b "

    # Pound
    # -----
    PS1+="%B%F{$C_O}%0(#.#.$)%f%b "

    # Set sub-prompts
    # PS2 - secondary prompt (e.g. for loop)
    # PS3 - selection prompt
    # PS4 - execution trace prompt
    PS2="%B%F{green}%_%f%F{$C_O}>%f%b "
    PS3="%B%F{green}#%f%F{$C_O}?%f%b "
    PS4="%B%F{green}%N %I%f%F{$C_O}+%f%b "

    # Set window title
    case $TERM in
        *xterm*|*rxvt)
            print -Pn "\e]0;SH: [${title_host}] ${cwd_ln}${cwd_name}\a"
            ;;
    esac
}

prompt_blami_setup() {
    prompt_opts=(cr subst percent)

    # Color theme
    # -----------
    # 0 - outlines color for EUID >0
    # 1 - outlines color for EUID =0
    prompt_blami_color0=${1:-'blue'}
    prompt_blami_color1=${2:-'magenta'}

    # Set vcs_info style
    zstyle ':vcs_info:*' enable git hg
    zstyle ':vcs_info:*' use-simple true
    zstyle ':vcs_info:*' formats "±% %b %u"

    # Hook to re-calculate prompt
    autoload -Uz add-zsh-hook
    add-zsh-hook precmd prompt_blami_precmd
}

prompt_blami_help() {
  cat <<EOH
EOH
}

prompt_blami_preview() {
    prompt_preview_theme blami "$@"
}

prompt_blami_setup "$@"

# vim:set ft=zsh:
