# Created by blami <blami@blami.net>

# Prompt components explanation:
#
#  .- 24h time
#  |     .- date in format DD.MM
#  |     | .- day of the week (weekend is red)
#  |     | |
#  |     | |     .- user
#  |     | |     |   .- @ is underline for SSH connection
#  |     | |     |   |.- host (HOST_NICKNAME if set)
#  |     | |     |   ||   .- host health/readiness
#  |     | |     |   ||   |.- host OS
#  |     | |     |   ||   ||   .- environment (.env)
#  |     | |     |   ||   ||   |     |  .- shortened path
#  |     | |     |   ||   ||   |     |  |        .- vcs info
#  v     v v     v   vv   vv   v     v  v        v
# (23:55 31w12)-(user@host●L)-([env:][@]~..di/r[ ±master])
# [255 ][&2 ][@]dir $ _                                                 -I-
# ^     ^    ^  ^   ^
# |     |    |  |   `- pound (EUID=0 is #)
# |     |    |  `- last path component
# |     |    `- symlink
# |     `- background jobs
# `- non-zero exit code


# Dependencies
zmodload -i zsh/datetime
autoload -Uz vcs_info
autoload -Uz cwdlast cwdellipse

function zle-line-init zle-keymap-select {
    # Color setup for outlines
    local C_O=${prompt_blami_color0}
    [ $EUID -eq 0 ] && C_O=${prompt_blami_color1}

    case $KEYMAP in
        vicmd)
            RPROMPT="%F{$C_O}N%f"
            print -rn -- $terminfo[cvvis]
            ;;
        viins|main)
            RPROMPT="%F{green}I%f"
            print -rn -- $terminfo[cnorm]
            ;;
    esac
    zle reset-prompt
    zle && zle -R
}

TRAPWINCH() {
  zle reset-prompt
  zle &&  zle -R
}

zle -N zle-line-init
zle -N zle-keymap-select
export KEYTIMEOUT=1


prompt_blami_precmd() {
    setopt localoptions noxtrace

    # Color setup for outlines
    local C_O=${prompt_blami_color0}
    [ $EUID -eq 0 ] && C_O=${prompt_blami_color1}

    # New-line
    local NL=$'\n'
    # Section begin, end and separator
    local SB="%F{${C_O}}(%f"
    local SE="%F{${C_O}})%f"
    local SS="%F{${C_O}}-%f"

    PS1=${NL}

    # Date
    # ----
    local dow="umtwhfs" ; dow=${dow:$(strftime %w $EPOCHSECONDS):1}
    case $dow in
        s|u) dow="%F{magenta}${dow}%f" ;;
        *) dow="%F{cyan}${dow}%f" ;;
    esac
    PS1+="${SB}%F{${C_O}}%D{%H:%M %d}%f${dow}%F{$C_O}%D{%m}%f${SE}"

    # User and hostname
    # -----------------
    local host=${HOSTNICK:-$HOST}
    [ ! -z $SSH_CONNECTION ] && host="%U@%u${host}" || host="@${host}"
    local host_ok=
    case $HOSTOK in
        0) host_ok="%F{green}●%f" ;;
        1) host_ok="%F{red}●%f" ;;
    esac
    local host_os="%F{white}${OS:0:1:u}%f"
    PS1+="${SS}${SB}%F{green}%n${host}%f${host_ok}${host_os}${SE}"

    # Working directory
    # -----------------
    # NOTE This is applied below in Working directory name section too
    local cwd_ln=
    [ -L $PWD ] && cwd_ln="@"
    local cwd_color=cyan
    [ ! -w $PWD ] && cwd_color=magenta
    local cwd_name=$(cwdlast)

    # Handle prompt length better, this can still overlap
    if [ $COLUMNS -gt 80 ]; then
        # Environment (.env)
        local env=

        # Current working directory elipsized
        local cwd="%F{$cwd_color}${cwd_ln}$(cwdellipse)%f"

        # VCS
        local vcs=
        #vcs_info
        #[ ! -z ${vcs_info_msg_0_} ] && vcs=" ${vcs_info_msg_0_}"

        PS1+="${SS}${SB}${env}${cwd}${vcs}${SE}"
    fi

    PS1+=${NL}

    # Return value
    # ------------
    PS1+="%(?..%F{red}%? %f)"

    # Background jobs
    # ---------------
    PS1+="%1(j.%F{$C_O}&%j %f.)"

    # Working directory name
    # ----------------------
    PS1+="%F{$cwd_color}${cwd_ln}${cwd_name}%f "

    # Pound
    # -----
    PS1+="%F{$C_O}%0(#.#.$)%f "


    # Set sub-prompts
    PS2=
    PS3=
    PS4=
    #RPS1=
}

prompt_blami_setup() {
    prompt_opts=(cr subst percent)

    # Color theme
    # -----------
    # 0 - outlines color for EUID >0
    # 1 - outlines color for EUID =0
    prompt_blami_color0=${1:-'blue'}
    prompt_blami_color1=${2:-'magenta'}

    # Set vcs_info style
    zstyle ':vcs_info:*' enable git hg
    zstyle ':vcs_info:*' use-simple true
    zstyle ':vcs_info:*' formats "±% %b %u"

    # Hook to re-calculate prompt
    autoload -Uz add-zsh-hook
    add-zsh-hook precmd prompt_blami_precmd
}

prompt_blami_help() {
  cat <<EOH
EOH
}

prompt_blami_preview() {
    prompt_preview_theme blami "$@"
}

prompt_blami_setup "$@"

# vim:set ft=zsh:
